%%[
  /* --- Querystring --- */
  SET @selectedCategoryId       = RequestParameter("cat")
  SET @selectedDataExtensionKey = RequestParameter("de")
  SET @countryCode              = RequestParameter("country")
  SET @languageCode             = RequestParameter("lang")
  SET @variantType              = RequestParameter("type")
  SET @variantVersion           = RequestParameter("version")

  /* Action params */
  SET @action   = RequestParameter("action")     /* 'activate', 'deactivate', 'save' */
  SET @targetCv = RequestParameter("cv")         /* target ContentVersion (also "current cv") */

  /* --- Category name --- */
  IF NOT EMPTY(@selectedCategoryId) THEN
    SET @selectedCategoryName = Lookup(
      "CM_ContentCategory",
      "CategoryName",
      "CategoryId",
      @selectedCategoryId
    )
  ENDIF

  /* --- Field config for this DE (used for form + save) --- */
  SET @contentFieldRows = LookupOrderedRows(
    "CM_ContentFieldConfig",
    0,
    "SortOrder ASC",
    "DataExtensionKey",
    @selectedDataExtensionKey
  )
  SET @contentFieldCount = RowCount(@contentFieldRows)

  SET @rowsUpdated = 0

  /* ------------------------------------------------------------------
     HANDLE ACTIONS: ACTIVATE / DEACTIVATE / SAVE (update current CV)
     ------------------------------------------------------------------ */
  IF NOT EMPTY(@action) AND
     NOT EMPTY(@selectedDataExtensionKey) AND
     NOT EMPTY(@countryCode) AND NOT EMPTY(@languageCode) AND
     NOT EMPTY(@variantType) AND NOT EMPTY(@variantVersion) THEN

    /* --- Activate selected version --- */
    IF @action == "activate" AND NOT EMPTY(@targetCv) THEN

      /* Deactivate any active versions for this variant */
      SET @activeRows = LookupRows(
        @selectedDataExtensionKey,
        "IsDeleted",   "0",
        "CountryCode",  @countryCode,
        "LanguageCode", @languageCode,
        "Type",         @variantType,
        "Version",      @variantVersion,
        "Status",       "Active"
      )
      SET @activeCount = RowCount(@activeRows)

      IF @activeCount > 0 THEN
        FOR @i = 1 TO @activeCount DO
          SET @row = Row(@activeRows, @i)
          SET @cv  = Field(@row, "ContentVersion")

          UpdateData(
            @selectedDataExtensionKey,
            5,
            "CountryCode",     @countryCode,
            "LanguageCode",    @languageCode,
            "Type",            @variantType,
            "Version",         @variantVersion,
            "ContentVersion",  @cv,
            "Status",          "Inactive",
            "ModifiedDate",    Now()
          )
        NEXT @i
      ENDIF

      /* Set target version to Active */
      UpdateData(
        @selectedDataExtensionKey,
        5,
        "CountryCode",     @countryCode,
        "LanguageCode",    @languageCode,
        "Type",            @variantType,
        "Version",         @variantVersion,
        "ContentVersion",  @targetCv,
        "Status",          "Active",
        "ModifiedDate",    Now()
      )

    /* --- Deactivate selected version --- */
    ELSEIF @action == "deactivate" AND NOT EMPTY(@targetCv) THEN

      UpdateData(
        @selectedDataExtensionKey,
        5,
        "CountryCode",     @countryCode,
        "LanguageCode",    @languageCode,
        "Type",            @variantType,
        "Version",         @variantVersion,
        "ContentVersion",  @targetCv,
        "Status",          "Inactive",
        "ModifiedDate",    Now()
      )

    /* --- SAVE: update currently selected ContentVersion (no auto-draft yet) --- */
    ELSEIF @action == "save" THEN

      /* Which CV are we editing? Comes from hidden input "cv" / querystring */
      SET @saveCv = @targetCv

      /* Fallback: if cv missing, use latest for this variant */
      IF EMPTY(@saveCv) THEN
        SET @latestForSaveRows = LookupOrderedRows(
          @selectedDataExtensionKey,
          1,
          "ContentVersion DESC",
          "IsDeleted",   "0",
          "CountryCode",  @countryCode,
          "LanguageCode", @languageCode,
          "Type",         @variantType,
          "Version",      @variantVersion
        )

        IF RowCount(@latestForSaveRows) > 0 THEN
          SET @latestForSaveRow = Row(@latestForSaveRows, 1)
          SET @saveCv           = Field(@latestForSaveRow, "ContentVersion")
        ENDIF
      ENDIF

      /* Loop config fields and update values on this CV */
      IF @contentFieldCount > 0 AND NOT EMPTY(@saveCv) THEN

        FOR @sf = 1 TO @contentFieldCount DO
          SET @saveFieldRow  = Row(@contentFieldRows, @sf)
          SET @saveFieldName = Field(@saveFieldRow, "FieldName")
          SET @saveValue     = RequestParameter(@saveFieldName)

          IF NOT EMPTY(@saveFieldName) THEN
            SET @r = UpdateData(
              @selectedDataExtensionKey,
              5,
              "CountryCode",     @countryCode,
              "LanguageCode",    @languageCode,
              "Type",            @variantType,
              "Version",         @variantVersion,
              "ContentVersion",  @saveCv,
              @saveFieldName,    @saveValue
            )
            SET @rowsUpdated = Add(@rowsUpdated, @r)
          ENDIF
        NEXT @sf

        /* Update ModifiedDate once */
        UpdateData(
          @selectedDataExtensionKey,
          5,
          "CountryCode",     @countryCode,
          "LanguageCode",    @languageCode,
          "Type",            @variantType,
          "Version",         @variantVersion,
          "ContentVersion",  @saveCv,
          "ModifiedDate",    Now()
        )
      ENDIF

    ENDIF
  ENDIF

  /* ------------------------------------------------------------------
     AFTER ACTIONS: load versions + pick which CV to edit
     ------------------------------------------------------------------ */
  SET @versionRowCount      = 0
  SET @latestContentVersion = ""
  SET @latestStatus         = ""
  SET @activeContentVersion = ""
  SET @contentVersion       = ""
  SET @currentRow           = ''

  IF NOT EMPTY(@selectedDataExtensionKey) AND NOT EMPTY(@countryCode) AND
     NOT EMPTY(@languageCode) AND NOT EMPTY(@variantType) AND NOT EMPTY(@variantVersion) THEN

    /* All versions for this variant */
    SET @versionRows = LookupOrderedRows(
      @selectedDataExtensionKey,
      0, /* all rows */
      "ContentVersion DESC",
      "IsDeleted",   "0",
      "CountryCode",  @countryCode,
      "LanguageCode", @languageCode,
      "Type",         @variantType,
      "Version",      @variantVersion
    )
    SET @versionRowCount = RowCount(@versionRows)

    /* Latest = first row */
    IF @versionRowCount > 0 THEN
      SET @latestRow            = Row(@versionRows, 1)
      SET @latestContentVersion = Field(@latestRow, "ContentVersion")
      SET @latestStatus         = Field(@latestRow, "Status")
    ENDIF

    /* Active CV (if any) */
    SET @activeRows = LookupOrderedRows(
      @selectedDataExtensionKey,
      1,
      "ContentVersion DESC",
      "IsDeleted",   "0",
      "CountryCode",  @countryCode,
      "LanguageCode", @languageCode,
      "Type",         @variantType,
      "Version",      @variantVersion,
      "Status",       "Active"
    )

    IF RowCount(@activeRows) > 0 THEN
      SET @activeRow            = Row(@activeRows, 1)
      SET @activeContentVersion = Field(@activeRow, "ContentVersion")
    ENDIF

    /* Which CV should we edit right now? */
    IF NOT EMPTY(@targetCv) THEN
      /* If a cv is in the URL / form, use that */
      SET @contentVersion = @targetCv
    ELSEIF NOT EMPTY(@activeContentVersion) THEN
      /* Otherwise, prefer active version */
      SET @contentVersion = @activeContentVersion
    ELSE
      /* Fallback: latest */
      SET @contentVersion = @latestContentVersion
    ENDIF

    /* Load the row for the current CV we are editing */
    IF NOT EMPTY(@contentVersion) THEN
      SET @currentRows = LookupRows(
        @selectedDataExtensionKey,
        "IsDeleted",   "0",
        "CountryCode",  @countryCode,
        "LanguageCode", @languageCode,
        "Type",         @variantType,
        "Version",      @variantVersion,
        "ContentVersion", @contentVersion
      )

      IF RowCount(@currentRows) > 0 THEN
        SET @currentRow = Row(@currentRows, 1)
      ENDIF
    ENDIF
  ENDIF
]%%

<h1 class="card-title">%%=v(@selectedCategoryName)=%%</h1>

<div style="text-align: right; margin-bottom: 0.5rem">
  <a
    class="btn-link"
    href="%%=Concat(
            CloudPagesURL(3637),
            '?cat=', @selectedCategoryId,
            '&de=',  @selectedDataExtensionKey
          )=%%"
  >
    ↩ back
  </a>
</div>

<!-- section 1 - static details table -->
<table class="list" style="margin-bottom: 1rem">
  <tbody>
    <tr>
      <th>Data Extension:</th>
      <td>%%=v(@selectedDataExtensionKey)=%%</td>
    </tr>
    <tr>
      <th>Country:</th>
      <td>%%=v(@countryCode)=%%</td>
    </tr>
    <tr>
      <th>Language:</th>
      <td>%%=v(@languageCode)=%%</td>
    </tr>
    <tr>
      <th>Type:</th>
      <td>%%=v(@variantType)=%%</td>
    </tr>
    <tr>
      <th>Version:</th>
      <td>%%=v(@variantVersion)=%%</td>
    </tr>
    <tr>
      <th>Currently editing CV:</th>
      <td>%%=IIF(EMPTY(@contentVersion),'–',@contentVersion)=%%</td>
    </tr>

    <!-- Active (live) version -->
    <tr>
      <th>Active content version:</th>
      <td>%%=IIF(EMPTY(@activeContentVersion),'–',@activeContentVersion)=%%</td>
    </tr>

    <!-- Latest (most recently saved) version -->
    <tr>
      <th>Latest content version:</th>
      <td>%%=IIF(EMPTY(@latestContentVersion),'–',@latestContentVersion)=%%</td>
    </tr>
    <tr>
      <th>Latest status:</th>
      <td>%%=IIF(EMPTY(@latestStatus),'–',@latestStatus)=%%</td>
    </tr>
  </tbody>
</table>

<!-- section 2 - middle editable field table (from config DE) -->
<form
  method="post"
  action="%%=Concat(
            CloudPagesURL(3638),
            '?cat=',     @selectedCategoryId,
            '&de=',      @selectedDataExtensionKey,
            '&country=', @countryCode,
            '&lang=',    @languageCode,
            '&type=',    @variantType,
            '&version=', @variantVersion,
            '&cv=',      @contentVersion
          )=%%"
>
  <!-- Trigger flag -->
  <input type="hidden" name="action" value="save" />
  <!-- Pass current CV back on save -->
  <input type="hidden" name="cv" value="%%=v(@contentVersion)=%%" />

  <table class="list" style="margin-bottom: 1rem">
    <thead>
      <tr>
        <th>Feld (Field)</th>
        <th>Content</th>
      </tr>
    </thead>
    <tbody>
      %%[
        IF @contentFieldCount > 0 AND @versionRowCount > 0 AND NOT EMPTY(@currentRow) THEN

          FOR @k = 1 TO @contentFieldCount DO
            SET @contentFieldRow  = Row(@contentFieldRows, @k)
            SET @contentFieldName = Field(@contentFieldRow, "FieldName")
            SET @fieldLength      = Field(@contentFieldRow, "FieldLength")

            /* Default length */
            IF EMPTY(@fieldLength) THEN
              SET @fieldLength = 255
            ENDIF

            /* Decide control type + height from FieldLength */
            SET @inputType = "text"
            SET @rows      = 1

            IF @fieldLength > 255 THEN
              SET @inputType = "textarea"
              IF @fieldLength > 2000 THEN
                SET @rows = 6
              ELSE
                SET @rows = 4
              ENDIF
            ENDIF

            /* Current value from the CV we are editing */
            SET @value = Field(@currentRow, @contentFieldName)
      ]%%
      <tr>
        <td>%%=v(@contentFieldName)=%%</td>
        <td>
          %%[ IF @inputType == "textarea" THEN ]%%
            <textarea
              name="%%=v(@contentFieldName)=%%"
              rows="%%=v(@rows)=%%"
              style="width: 100%;"
            >%%=v(@value)=%%</textarea>
          %%[ ELSE ]%%
            <input
              type="text"
              name="%%=v(@contentFieldName)=%%"
              value="%%=v(@value)=%%"
              style="width: 100%;"
            />
          %%[ ENDIF ]%%
        </td>
      </tr>
      %%[
          NEXT @k

        ELSE
      ]%%
      <tr>
        <td colspan="2">No fields configured or no versions found.</td>
      </tr>
      %%[ ENDIF ]%%
    </tbody>
  </table>

  <div style="text-align:center; margin-bottom:1rem;">
    <!-- SAVE: submit the form (posts fields + action=save) -->
    <button class="btn btn-secondary" type="submit">
      save
    </button>

    <!-- DISCARD: just reload variant without saving -->
    <button
      class="btn btn-secondary"
      type="button"
      onclick="window.location.href='%%=Concat(
        CloudPagesURL(3638),
        '?cat=',     @selectedCategoryId,
        '&de=',      @selectedDataExtensionKey,
        '&country=', @countryCode,
        '&lang=',    @languageCode,
        '&type=',    @variantType,
        '&version=', @variantVersion
      )=%%'"
    >
      discard changes
    </button>
  </div>
</form>

<!-- section 3 - version history -->
<table class="list">
  <thead>
    <tr>
      <th>Content Version</th>
      <th>Status</th>
      <th>CreatedDate</th>
      <th>Last Modified Date</th>
      <th class="actions">Action</th>
    </tr>
  </thead>
  <tbody>
    %%[
      IF @versionRowCount > 0 THEN

        FOR @i = 1 TO @versionRowCount DO
          SET @row            = Row(@versionRows, @i)
          SET @cv             = Field(@row, "ContentVersion")
          SET @cvStatus       = Field(@row, "Status")
          SET @cvCreatedDate  = Field(@row, "CreatedDate")
          SET @cvLastModified = Field(@row, "ModifiedDate")

          /* Build base URL: selecting this CV */
          SET @baseUrl = Concat(
                          CloudPagesURL(3638),
                          '?cat=',     @selectedCategoryId,
                          '&de=',      @selectedDataExtensionKey,
                          '&country=', @countryCode,
                          '&lang=',    @languageCode,
                          '&type=',    @variantType,
                          '&version=', @variantVersion,
                          '&cv=',      @cv
                        )
          SET @activateUrl   = Concat(@baseUrl, '&action=activate')
          SET @deactivateUrl = Concat(@baseUrl, '&action=deactivate')
    ]%%
    <tr>
      <td>
        %%=v(@cv)=%%
        %%[ IF @cv == @contentVersion THEN ]%%
          <span style="font-size:0.8rem; color:#555;">(current)</span>
        %%[ ENDIF ]%%
      </td>
      <td>%%=v(@cvStatus)=%%</td>
      <td>%%=v(@cvCreatedDate)=%%</td>
      <td>%%=v(@cvLastModified)=%%</td>
      <td class="actions">
        %%[ IF @cvStatus == "Active" THEN ]%%
          <a
            href="%%=v(@deactivateUrl)=%%"
            class="btn-link"
            onclick="return confirm('Deactivate version %%=v(@cv)=%%?');"
          >
            Deactivate
          </a>
        %%[ ELSE ]%%
          <a
            href="%%=v(@activateUrl)=%%"
            class="btn-link"
            onclick="return confirm('Activate version %%=v(@cv)=%%?');"
          >
            Activate
          </a>
        %%[ ENDIF ]%%
      </td>
    </tr>
    %%[
        NEXT @i

      ELSE
    ]%%
    <tr>
      <td colspan="5">No versions found for this variant.</td>
    </tr>
    %%[ ENDIF ]%%
  </tbody>
</table>
